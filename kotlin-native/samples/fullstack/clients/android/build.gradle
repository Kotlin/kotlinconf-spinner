buildscript {

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:+"
        classpath 'com.android.tools.build:gradle-experimental:0.9.2'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

project.ext.libcurlPath = file('../../../../external/curl/android')
project.ext.janssonPath = file('../../../../external/jansson/android')
project.ext.openalPath = file('../../../../external/openal/android')

apply plugin: "konan"


def platforms = [
    [name: "arm32", konanTarget: "android_arm32", targetPlatform: "armeabi-v7a", artifact: "Kotlin3dArm32"],
    [name: "arm64", konanTarget: "android_arm64", targetPlatform: "arm64-v8a", artifact: "Kotlin3dArm64"]
]


konanInterop {
    platforms.each { platform ->
        "platform_${platform.name}" {
            defFile "android.def"
            pkg "android"
            // TODO: launcher header will be part of the platform library.
            includeDirs "${project.ext.openalPath}/include"
            includeDirs "."
            target platform.konanTarget
        }

        "common_${platform.name}" {
            defFile "../../common/common.def"
            pkg "common"
            //includeDirs  ""
            target platform.konanTarget
        }

        "jansson_${platform.name}" {
            defFile "../../json/jansson.def"
            pkg "jansson"
            includeDirs  "${project.ext.janssonPath}/${platform.name}/include"
            target platform.konanTarget
        }

        "libcurl_${platform.name}" {
            defFile "../../kurl/libcurl.def"
            pkg "libcurl"
            includeDirs  "${project.ext.libcurlPath}/include"
            target platform.konanTarget
        }
    }
}

konanArtifacts {
    platforms.each { platform ->
        "Kommon_${platform.name}" {
            useInterop "common_${platform.name}"
            inputFiles "../../common/Kommon.kt", "../../common/KommonLinux.kt"
            produce "library"
            extraOpts "-r", "build/konan/bin", "-r", "build/konan/c_interop"
            target platform.konanTarget
        }

        "KJson_${platform.name}" {
            useInterop "jansson_${platform.name}"
            inputFiles "../../json/KJson.kt"
            library konanArtifacts["Kommon_${platform.name}"].compilationTask.artifact
            dependsOn(konanArtifacts["Kommon_${platform.name}"].compilationTask)
            produce "library"
            extraOpts "-r", "build/konan/bin", "-r", "build/konan/c_interop"
            target platform.konanTarget
        }

        "Kurl_${platform.name}" {
            useInterop "common_${platform.name}"
            useInterop "libcurl_${platform.name}"
            inputFiles "../../kurl/KUrl.kt"
            library konanArtifacts["Kommon_${platform.name}"].compilationTask.artifact
            dependsOn(konanArtifacts["Kommon_${platform.name}"].compilationTask)
            produce "library"
            extraOpts "-r", "build/konan/bin", "-r", "build/konan/c_interop"
            target platform.konanTarget
        }

        "$platform.artifact" {
            useInterop "platform_${platform.name}"
            library konanArtifacts["Kommon_${platform.name}"].compilationTask.artifact
            library konanArtifacts["KJson_${platform.name}"].compilationTask.artifact
            library konanArtifacts["Kurl_${platform.name}"].compilationTask.artifact
            library konanInterop["common_${platform.name}"].interopProcessingTask.klib
            library konanInterop["jansson_${platform.name}"].interopProcessingTask.klib
            library konanInterop["libcurl_${platform.name}"].interopProcessingTask.klib
            dependsOn(konanArtifacts["Kommon_${platform.name}"].compilationTask)
            dependsOn(konanArtifacts["KJson_${platform.name}"].compilationTask)
            dependsOn(konanArtifacts["Kurl_${platform.name}"].compilationTask)
            linkerOpts "-L${project.ext.libcurlPath}/lib/${platform.targetPlatform} -lcurl ${project.ext.janssonPath}/${platform.name}/lib/libjansson.a -lopenal -L${project.ext.openalPath}/lib/${platform.targetPlatform}"
            target platform.konanTarget
            extraOpts "-r", "build/konan/bin", "-r", "build/konan/c_interop"
            outputDir platform.artifact
            outputName 'libkotlin3d'
            enableOptimization()
        }
    }
}



apply plugin: "com.android.model.application"

model {
    android {
        compileSdkVersion = 25
        buildToolsVersion = '25.0.2'

        defaultConfig {
            applicationId = 'com.jetbrains.konan_activity'
            minSdkVersion.apiLevel   9
            targetSdkVersion.apiLevel  25
        }

        ndk {
            moduleName = "kotlin3d"
        }

        productFlavors {
            create("arm") {
                ndk {
                    abiFilters.addAll(platforms.collect { it.targetPlatform })
                }
            }
        }

        buildTypes {
            release {
                signingConfig = $("android.signingConfigs.release")
            }
        }
    }

    android.signingConfigs {
        create("release") {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    repositories {
        libs(PrebuiltLibraries) {
            libkotlin3d {
                    binaries.withType(SharedLibraryBinary) {
                        def name = targetPlatform.getName()
                        def index = platforms.collect { it.targetPlatform }.indexOf(name)
                        if (index >= 0)
                            sharedLibraryFile = file("${platforms[index].artifact}/libkotlin3d.so")
                    }
            }
            libopenal {
                    binaries.withType(SharedLibraryBinary) {
                        def name = targetPlatform.getName()
                        def index = platforms.collect { it.targetPlatform }.indexOf(name)
                        if (index >= 0)
                            sharedLibraryFile = file("${project.ext.openalPath}/lib/${name}/libopenal.so")
                    }
            }
       }
    }
    android.sources {
        main {
            jniLibs {
                dependencies {
                    library "libkotlin3d"
                    library "libopenal"
                }
            }
        }
    }

}

tasks.matching { it.name == 'preBuild' }.all {
    it.dependsOn 'compileKonanKotlin3dArm32'
    it.dependsOn 'compileKonanKotlin3dArm64'
}

task buildApk(type: DefaultTask) {
    dependsOn "compileKonanKotlin3dArm32"
    dependsOn "compileKonanKotlin3dArm64"
    dependsOn "assembleDebug"
}
