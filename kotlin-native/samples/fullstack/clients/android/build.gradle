buildscript {

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:+"
        classpath 'com.android.tools.build:gradle-experimental:0.9.3'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

project.ext.libcurlPath = file('../../../../external/curl/android')
project.ext.janssonPath = file('../../../../external/jansson/android')
project.ext.openalPath = file('../../../../external/openal/android')

apply plugin: "konan"


def platforms = [
    [name: "arm32", konanTarget: "android_arm32", targetPlatform: "armeabi-v7a", artifact: "Kotlin3dArm32"],
    [name: "arm64", konanTarget: "android_arm64", targetPlatform: "arm64-v8a", artifact: "Kotlin3dArm64"]
]


konanInterop {
    platforms.each { platform ->
        "openal_${platform.name}" {
            defFile "openal.def"
            includeDirs "${project.ext.openalPath}/include"
            includeDirs "."
            target platform.konanTarget
        }

        "jansson_${platform.name}" {
            defFile "../../json/src/jansson.def"
            pkg "jansson"
            includeDirs  "${project.ext.janssonPath}/${platform.name}/include"
            target platform.konanTarget
        }

        "libcurl_${platform.name}" {
            defFile "../../kurl/src/libcurl.def"
            pkg "libcurl"
            includeDirs  "${project.ext.libcurlPath}/include"
            target platform.konanTarget
        }
    }
}

konanArtifacts {
    platforms.each { platform ->
        "Kommon_${platform.name}" {
            inputFiles "../../common/src/Kommon.kt", "../../common/src/KommonLinux.kt", "../../common/src/KommonAndroid.kt"
            produce "library"
            extraOpts "-r", "build/konan/bin", "-r", "build/konan/c_interop"
            target platform.konanTarget
        }

        "KJson_${platform.name}" {
            useInterop "jansson_${platform.name}"
            inputFiles "../../json/src/KJson.kt"
            library konanArtifacts["Kommon_${platform.name}"].compilationTask.artifact
            dependsOn(konanArtifacts["Kommon_${platform.name}"].compilationTask)
            produce "library"
            extraOpts "-r", "build/konan/bin", "-r", "build/konan/c_interop"
            target platform.konanTarget
        }

        "Kurl_${platform.name}" {
            useInterop "libcurl_${platform.name}"
            inputFiles "../../kurl/src/KUrl.kt"
            library konanArtifacts["Kommon_${platform.name}"].compilationTask.artifact
            dependsOn(konanArtifacts["Kommon_${platform.name}"].compilationTask)
            produce "library"
            extraOpts "-r", "build/konan/bin", "-r", "build/konan/c_interop"
            target platform.konanTarget
        }

        "Loader_${platform.name}" {
            target platform.konanTarget
            inputDir "src/loader"
            extraOpts "-r", "build/konan/bin", "-r", "build/konan/c_interop"
            outputDir platform.artifact
            outputName 'libloader'
        }

        "$platform.artifact" {
            useInterop "openal_${platform.name}"
            library konanArtifacts["Kommon_${platform.name}"].compilationTask.artifact
            library konanArtifacts["KJson_${platform.name}"].compilationTask.artifact
            library konanArtifacts["Kurl_${platform.name}"].compilationTask.artifact
            library konanInterop["jansson_${platform.name}"].interopProcessingTask.klib
            library konanInterop["libcurl_${platform.name}"].interopProcessingTask.klib
            dependsOn(konanArtifacts["Kommon_${platform.name}"].compilationTask)
            dependsOn(konanArtifacts["KJson_${platform.name}"].compilationTask)
            dependsOn(konanArtifacts["Kurl_${platform.name}"].compilationTask)
            linkerOpts "-L${project.ext.libcurlPath}/lib/${platform.targetPlatform} -lcurl ${project.ext.janssonPath}/${platform.name}/lib/libjansson.a -L${project.ext.openalPath}/lib/${platform.targetPlatform} -lopenal"
            target platform.konanTarget
            extraOpts "-r", "build/konan/bin", "-r", "build/konan/c_interop"
            outputDir platform.artifact
            outputName 'libkotlin3d'
            inputFiles fileTree('../shared/src/main/kotlin')
            inputFiles fileTree('src/main/kotlin')
            enableOptimization()
        }
    }
}



apply plugin: "com.android.model.application"

model {
    android {
        compileSdkVersion = 25
        buildToolsVersion = '25.0.2'

        defaultConfig {
            applicationId = 'com.jetbrains.konan_activity'
            minSdkVersion.apiLevel   18
            targetSdkVersion.apiLevel  25
        }

        ndk {
            moduleName = "kotlin3d"
        }

        productFlavors {
            create("arm") {
                ndk {
                    abiFilters.addAll(platforms.collect { it.targetPlatform })
                }
            }
        }

        buildTypes {
            debug {
               debuggable = true
               ndk {
                 debuggable = true
               }
            }
            release {
                // Add RELEASE_STORE_FILE RELEASE_STORE_PASSWORD RELEASE_KEY_ALIAS
                // and RELEASE_KEY_PASSWORD to ~/.gradle/gradle.properties
                // to sign release build. Keystore could be created from
                // Android Studio.
                if (project.hasProperty("RELEASE_STORE_FILE")) {
                   signingConfig = $("android.signingConfigs.release")
                }
            }
        }
}

    android.signingConfigs {
      create("release") {
        if (project.hasProperty("RELEASE_STORE_FILE")) {
            println(project.getProperty("RELEASE_STORE_FILE"))
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
      }
    }

    repositories {
        libs(PrebuiltLibraries) {
            libkotlin3d {
                    binaries.withType(SharedLibraryBinary) {
                        def name = targetPlatform.getName()
                        def index = platforms.collect { it.targetPlatform }.indexOf(name)
                        if (index >= 0)
                            sharedLibraryFile = file("${platforms[index].artifact}/libkotlin3d.so")
                    }
            }
            libloader {
                binaries.withType(SharedLibraryBinary) {
                    def name = targetPlatform.getName()
                    def index = platforms.collect { it.targetPlatform }.indexOf(name)
                    if (index >= 0)
                        sharedLibraryFile = file("${platforms[index].artifact}/libloader.so")
                }
            }
            libopenal {
                    binaries.withType(SharedLibraryBinary) {
                        def name = targetPlatform.getName()
                        def index = platforms.collect { it.targetPlatform }.indexOf(name)
                        if (index >= 0)
                            sharedLibraryFile = file("${project.ext.openalPath}/lib/${name}/libopenal.so")
                    }
            }
       }
    }
    android.sources {
        main {
            jniLibs {
                dependencies {
                    library "libkotlin3d"
                    library "libloader"
                    library "libopenal"
                }
            }
        }
    }

}

tasks.matching { it.name == 'preBuild' }.all {
    platforms.each { platform ->
        it.dependsOn "compileKonanLoader_${platform.name}"
        it.dependsOn "compileKonan${platform.artifact}"
    }
}

task buildApk(type: DefaultTask) {
    dependsOn "assembleDebug"
}
